plugins {
  id 'java'
  id 'org.springframework.boot' version '3.3.4'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'org.flywaydb.flyway' version '9.8.1'
  id 'jacoco'
}

group = 'ru.ifmo.is'
version = file('./version').text.trim()

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

dependencies {
  // Spring
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework:spring-websocket:6.1.13'

  // JSON
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5-jakarta:2.18.0'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
  implementation 'com.networknt:json-schema-validator:1.5.1'

  // Lombok
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  // MapStruct
  implementation 'org.mapstruct:mapstruct:1.6.2'
  implementation 'org.projectlombok:lombok-mapstruct-binding:0.1.0'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

  // MinIO
  implementation 'io.minio:minio:8.5.14'

  // JJWT
  implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

  // Database
  runtimeOnly 'org.postgresql:postgresql'
  implementation 'org.hibernate:hibernate-core:6.6.0.Final'

  // Utils
  implementation 'org.apache.commons:commons-lang3:3.17.0'
  implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'
  implementation 'commons-io:commons-io:2.16.1'

  // Flyway
  implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-database-postgresql'
  runtimeOnly 'org.flywaydb:flyway-gradle-plugin:10.18.2'

  // Swagger UI
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

  // DevTools
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  // Testing
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
}

jacoco {
  toolVersion = '0.8.12'
}

jacocoTestReport {
  dependsOn test
  finalizedBy jacocoTestCoverageVerification
  reports {
    xml.required = true
    html.required = true
  }
}

jacocoTestCoverageVerification {
  dependsOn test
  violationRules {
    rule {
      limit {
        minimum = 0.0
      }
    }
  }
}

tasks.named('test') {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}
